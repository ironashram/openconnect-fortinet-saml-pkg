# Maintainer: Michele Palazzi <sysdadmin@m1k.cloud>
# Based on the Arch openconnect PKGBUILD by Levente Polyak

pkgname=openconnect-fortinet-saml-git
_upstream=openconnect
pkgver=1.0.0
pkgrel=1
epoch=1
pkgdesc='Open client for Cisco AnyConnect VPN with Fortinet SAML/SSO support'
url='https://github.com/ironashram/openconnect-fortinet-saml'
arch=('x86_64')
license=('LGPL-2.1-only')
depends=('libxml2' 'gnutls' 'libproxy' 'vpnc' 'krb5' 'lz4' 'pcsclite'
         'stoken' 'tpm2-tss' 'oath-toolkit' 'libproxy' 'libp11-kit'
         'xdg-utils'
         libstoken.so libtss2-esys.so libtss2-mu.so libtss2-tctildr.so
         libxml2.so libproxy.so libhogweed.so libp11-kit.so libpskc.so
         libgssapi_krb5.so libpcsclite.so)
makedepends=('git' 'intltool' 'python' 'autoconf' 'automake' 'libtool' 'pkgconf')
checkdepends=('python-flask')
optdepends=('python: tncc-wrapper')
provides=('openconnect' 'libopenconnect.so')
conflicts=('openconnect')
options=('!emptydirs')
source=("${_upstream}::git+https://github.com/ironashram/openconnect-fortinet-saml.git#branch=master")
sha256sums=('SKIP')

prepare() {
  cd "${_upstream}"
  ./autogen.sh
}

build() {
  cd "${_upstream}"
  PYTHON=/usr/bin/python \
    ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib \
    --disable-static
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd "${_upstream}"
  make check
}

package() {
  cd "${_upstream}"
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
